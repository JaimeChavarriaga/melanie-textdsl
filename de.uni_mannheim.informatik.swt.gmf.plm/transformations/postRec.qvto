modeltype GMFGEN uses gmfgen('http://www.eclipse.org/gmf/2009/GenModel');
	
transformation postRec(inout gmfgenModel : GMFGEN);

property genNavigator : GenNavigator = null;
 
main() {
	this.genNavigator := gmfgenModel.objects()[GenNavigator]->asOrderedSet()->first();

	--Clear all child entries
	this.genNavigator.childReferences := OrderedSet{};

	--Root group for the LMLModel container
	var LMLModelChildReference := new GenNavigatorChildReference();
	LMLModelChildReference.child := gmfgenModel.objects()[GenDiagram]->asOrderedSet()->select(n | n.editPartClassName = "LMLModelEditPart")->first();
	
	--Group for all DomainEntities that contain Ontology rendering information
	var rootDomainEnitiyChildReference := this.createChildReference("DomainEntityEditPart", "LMLModelEditPart", "Domain Entities",  "icons/clabject16.gif");
	rootDomainEnitiyChildReference.parent := gmfgenModel.objects()[GenDiagram]->asOrderedSet()->select(n | n.editPartClassName = "LMLModelEditPart")->first();
	
	--Group for all Ontologies
	var ontologyChildReference := this.createChildReference("OntologyEditPart", "LMLModelEditPart", "Ontologies", "icons/onthology16.gif");
	ontologyChildReference.parent := gmfgenModel.objects()[GenDiagram]->asOrderedSet()->select(n | n.editPartClassName = "LMLModelEditPart")->first();
	--Group for all Models
	var modelChildReference := this.createChildReference("ClabjectEditPart", "OntologyEditPart", "Models", "icons/model16.gif");
	
	this.genNavigator.childReferences += OrderedSet{LMLModelChildReference, rootDomainEnitiyChildReference, ontologyChildReference, modelChildReference};
}

mapping createChildReference(childEditPartName:String, parentEditPartName:String, groupName:String, icon:String): GenNavigatorChildReference
{
	result.child := gmfgenModel.objects()[GenNode]->asOrderedSet()->select(n | n.editPartClassName = childEditPartName)->first();
	result.parent := gmfgenModel.objects()[GenNode]->asOrderedSet()->select(n | n.editPartClassName = parentEditPartName)->first();
	result.hideIfEmpty := false;
	result.groupName := groupName;
	result.groupIcon := icon;
}