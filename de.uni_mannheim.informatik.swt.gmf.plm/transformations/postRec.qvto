modeltype GMFGEN uses gmfgen('http://www.eclipse.org/gmf/2009/GenModel');
	
transformation postRec(inout gmfgenModel : GMFGEN);

property genNavigator : GenNavigator = null;
property genStandardPrefencePage : GenStandardPreferencePage = null;
property genPlugIn : GenPlugin = null;
property genEditorGenerator : GenEditorGenerator = null;
property genDiagram: GenDiagram = null;
		
 
main() {
	
	--Needed for Attribute/Method Resize when renderers are shown/hidden
	gmfgenModel.objectsOfType(GenChildNode)->asOrderedSet()
		->select(c | c.editPartClassName.startsWith("Attribute") 
			or c.editPartClassName.startsWith("Method"))->forEach(node)
		{
			var defaultSize := new DefaultSizeAttributes();
			defaultSize.height := 22;
			defaultSize.width := 0;
			node.viewmap.attributes := Sequence {defaultSize};
		};
	
	--Set ListLayout for Model(Clabject) Compartment
	gmfgenModel.objectsOfType(GenCompartment)->asOrderedSet()
				->select(c | c.editPartClassName = "OntologyModelCompartmentEditPart")->first()
				.listLayout := true;
	
	gmfgenModel.objectsOfType(GenCompartment)->asOrderedSet()
				->select(c | c.editPartClassName.find("Attributes") > 0)->forEach(comp)
				{
					comp.listLayout := true;
				};
	
	gmfgenModel.objectsOfType(GenCompartment)->asOrderedSet()
				->select(c | c.editPartClassName.find("Methods") > 0)->forEach(comp)
				{
					comp.listLayout := true;
				};
	
	
	
	--Setup the GenEditorGenerator
	this.genEditorGenerator := gmfgenModel.objectsOfType(GenEditorGenerator)->asOrderedSet()->first();
	genEditorGenerator.sameFileForDiagramAndModel := true;
	genEditorGenerator.dynamicTemplates := true;
	genEditorGenerator.templateDirectory := "/de.uni_mannheim.informatik.swt.gmf.plm/templates";
	genEditorGenerator.diagramFileExtension := "lml";
	genEditorGenerator.modelID := "LML";
	
	--Setup the GenDiagram
	genDiagram
		:= gmfgenModel.objectsOfType(GenDiagram)->asOrderedSet()->first();
	genDiagram.validationEnabled := true;
	genDiagram.validationDecorators := true;
	genDiagram.validationProviderPriority := ProviderPriority::Medium;
	genDiagram.validationDecoratorProviderPriority := ProviderPriority::Medium;
	genDiagram.creationWizardCategoryID := "de.uni_mannheim.informatik.swt.lmlcategory";
	
	--Setup the GenPlugin
	this.genPlugIn := gmfgenModel.objectsOfType(GenPlugin)->asOrderedSet()->first();
	genPlugIn.provider := "University of Mannheim: Chair for Software Engineering";
	genPlugIn.name := "Level-agnostic Modelling Language (LML) Plugin";
	genPlugIn.printingEnabled := true;
	genPlugIn.requiredPlugins := genPlugIn.requiredPlugins->append('de.itemis.gmf.runtime.extensions');
	genPlugIn.requiredPlugins := genPlugIn.requiredPlugins->append('de.uni_mannheim.informatik.swt.gmf.plm');
	
	--Set the properties for the Prefrence Page (Window -> Preferences)
	this.genStandardPrefencePage := gmfgenModel.objectsOfType(GenStandardPreferencePage)->asOrderedSet()->first();
	genStandardPrefencePage.name := "LML Diagram Editor";
	
	--Build up the navigation structure
	this.genNavigator := gmfgenModel.objectsOfType(GenNavigator)->asOrderedSet()->first();

	--Clear all child entries
	this.genNavigator.childReferences := OrderedSet{};

	--Root group for the LMLModel container
	var LMLModelChildReference 
		:= new GenNavigatorChildReference();
	LMLModelChildReference.child 
		:= gmfgenModel.objectsOfType(GenDiagram)->asOrderedSet()->select(n | n.editPartClassName = "LMLModelEditPart")->first();
	
	--Group for all Entities that contain Ontology rendering information
	--var rootEnitiyChildReference 
	--	:= xmap createChildReference("EditPart", "LMLModelEditPart", "Entities",  "icons/clabject16.gif");
	--rootEnitiyChildReference.parent 
	--	:= gmfgenModel.objectsOfType(GenDiagram)->asOrderedSet()->select(n | n.editPartClassName = "LMLModelEditPart")->first();
	--s for Entities that describe rendering of ontologies
	--var rootChildReference 
	--	:= xmap createChildReference("EditPart", "EntityEditPart", "s", "icons/16.gif");
	--Methods for Entities that describe rendering of ontologies
	--var rootMethodChildReference 
	--	:= xmap createChildReference("MethodEditPart", "EntityEditPart", "Methods", "icons/method16.gif");
	
	--Group for all Ontologies
	var ontologyChildReference 
		:= xmap createChildReference("OntologyEditPart", "LMLModelEditPart", "Ontologies", "icons/ontology16.gif");
	ontologyChildReference.parent 
		:= gmfgenModel.objectsOfType(GenDiagram)->asOrderedSet()->select(n | n.editPartClassName = "LMLModelEditPart")->first();
	--Group for the renderers of the ontology
	var ontRendererChildReference 
		:= xmap createChildReference("Renderer10EditPart", "OntologyEditPart", "Renderers", "icons/renderer16.gif");
	
	--Group for all Models
	var modelChildReference 
		:= xmap createChildReference("ModelEditPart", "OntologyEditPart", "Models", "icons/model16.gif");
	--Group for all ModelRenderers
	var modelRendererChildReference 
		:= xmap createChildReference("Renderer2EditPart", "ModelEditPart", "Renderers", "icons/renderer16.gif");
	
	
	--Group for all Entities
	var entityChildReference 
		:= xmap createChildReference("EntityEditPart", "ModelEditPart", "Entities", "icons/clabject16.gif");
		--s for Entities that describe rendering of ontologies
	var entityAttributeChildReference 
		:= xmap createChildReference("Attribute2EditPart", "EntityEditPart", "Attributes", "icons/field16.gif");
	--Methods for Entities that describe rendering of ontologies
	var entityMethodChildReference
		:= xmap createChildReference("MethodEditPart", "EntityEditPart", "Methods", "icons/method16.gif");
	var entityRendererChildReference
		:= xmap createChildReference("Renderer9EditPart", "EntityEditPart", "Renderers", "icons/renderer16.gif");
	
	
	--Group for all Connections
	var connectionChildReference 
		:= xmap createChildReference("ConnectionEditPart", "ModelEditPart", "Connections", "icons/connection16.gif");
	--Group for all ConnectionAttributes
	var connectionAttributeChildReference 
		:= xmap createChildReference("AttributeEditPart", "ConnectionEditPart", "Attributes", "icons/field16.gif");
	--Group for all ConnectionRenderers
	var connectionRendererChildReference 
		:= xmap createChildReference("RendererEditPart", "ConnectionEditPart", "Renderers", "icons/renderer16.gif");
		
	--Group for all MultipleGeneralizations
	var multipleGeneralizationChildReference 
		:= xmap createChildReference("MultipleGeneralizationEditPart", "ModelEditPart", "Generalizations", "icons/inheritance16.gif");
	--Group for all MultipleGeneralizationRenderers
	var multipleGeneralizationRendererChildReference 
		:= xmap createChildReference("Renderer6EditPart", "MultipleGeneralizationEditPart", "Renderers", "icons/renderer16.gif");
	
		
	--Group for all MultipleSpecializations
	var multipleSpecializationChildReference 
		:= xmap createChildReference("MultipleSpecializationEditPart", "ModelEditPart", "Generalizations", "icons/inheritance16.gif");
	--Group for all MultipleSpecializationRenderers
	var multipleSpecializationRendererChildReference 
		:= xmap createChildReference("Renderer7EditPart", "MultipleSpecializationEditPart", "Renderers", "icons/renderer16.gif");
	
	
	--Group for all BinaryGeneralizations
	var binaryGeneralizationChildReference 
		:= xmap createChildReference("BinaryGeneralizationEditPart", "ModelEditPart", "Generalizations", "icons/inheritance16.gif");
	--Group for all BinaryGeneralizationRenderers
	var binaryGeneralizationRendererChildReference
		:= xmap createChildReference("Renderer8EditPart", "BinaryGeneralizationEditPart", "Renderers", "icons/renderer16.gif");
	
	--Group for all ComplementSetRelationships
	var complementChildReference 
		:= xmap createChildReference("ComplementEditPart", "ModelEditPart", "Set Relationships", "icons/setRelationship16.gif");
	--Group for all ComplementSetRelationshipRenderers
	var complementRendererChildReference 
		:= xmap createChildReference("Renderer3EditPart", "ComplementEditPart", "Renderers", "icons/renderer16.gif");
		
		
	--Group for all EqualSetRelationships
	var equalChildReference 
		:= xmap createChildReference("EqualityEditPart", "ModelEditPart", "Set Relationships", "icons/setRelationship16.gif");
	--Group for all EqualSetRelationships
	var equalRendererChildReference 
		:= xmap createChildReference("Renderer4EditPart", "EqualityEditPart", "Renderers", "icons/renderer16.gif");		
	
	--Group for all InversionSetRelationships
	var inversionChildReference 
		:= xmap createChildReference("InversionEditPart", "ModelEditPart", "Set Relationships", "icons/setRelationship16.gif");
	--Group for all InversionSetRelationshipRenderers
	var inversionRendererChildReference 
		:= xmap createChildReference("Renderer5EditPart", "InversionEditPart", "Renderers", "icons/renderer16.gif");
	
	this.genNavigator.childReferences += 
		OrderedSet{LMLModelChildReference, --rootEnitiyChildReference, rootChildReference, rootMethodChildReference,
					 ontologyChildReference, ontRendererChildReference, modelChildReference, modelRendererChildReference,
					entityChildReference, entityRendererChildReference, entityAttributeChildReference, 
					entityMethodChildReference, connectionChildReference, connectionRendererChildReference, 
					connectionAttributeChildReference, --connectionFieldMethodChildReference
					binaryGeneralizationChildReference, binaryGeneralizationRendererChildReference,
					multipleGeneralizationChildReference, multipleGeneralizationRendererChildReference, 
					multipleSpecializationChildReference, multipleSpecializationRendererChildReference,
					complementChildReference, complementRendererChildReference, 
					equalChildReference, equalRendererChildReference,
					inversionChildReference, inversionRendererChildReference};
	
	--fix BUG 331875
	gmfgenModel.objectsOfType(ExpressionLabelParser)->forEach(parser)
	{
		parser.className := parser.className.concat(parser._uses->first().container().oclAsType(GenNodeLabel).visualID.toString());
	};
}

mapping createChildReference(childEditPartName:String, parentEditPartName:String, groupName:String, icon:String): GenNavigatorChildReference
{
	result.child := gmfgenModel.objectsOfType(GenNode)->asOrderedSet()->select(n | n.editPartClassName = childEditPartName)->first();
	result.parent := gmfgenModel.objectsOfType(GenNode)->asOrderedSet()->select(n | n.editPartClassName = parentEditPartName)->first();
	result.hideIfEmpty := false;
	result.groupName := groupName;
	result.groupIcon := icon;
}